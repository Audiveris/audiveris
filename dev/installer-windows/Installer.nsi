; Script generated by the HM NIS Edit Script Wizard.

; Expected symbols
; ----------------
; PRODUCT_VERSION:  Product version
; PROJECT_DIR:      Project root directory
; TARGET_OS:        OS name and architecture (windows-x86 or windows-x86_64)
; SUFFIX:           Optional suffix ("32" or "") to cope with 32/64 coexisting installations
!ifndef SUFFIX
  !define SUFFIX ""
!endif

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Audiveris"
!define PRODUCT_PUBLISHER "Audiveris Team"
!define PRODUCT_WEB_SITE "http://www.audiveris.org"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}${SUFFIX}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

!include "LogicLib.nsh"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; code derived from "http://nsis.sourceforge.net/File_Association"
!include "FileAssociationWithIcon.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${PROJECT_DIR}\res\icon-256.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; License page
!insertmacro MUI_PAGE_LICENSE "${PROJECT_DIR}\LICENSE"

; Directory page
!insertmacro MUI_PAGE_DIRECTORY

; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Audiveris${SUFFIX}"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\Audiveris.bat"
!define MUI_FINISHPAGE_RUN_TEXT "Launch Audiveris"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${PROJECT_DIR}\build\installers\Audiveris_Setup-${PRODUCT_VERSION}-${TARGET_OS}.exe"
ShowInstDetails show
ShowUnInstDetails show

Function .onInit
    ${If} $INSTDIR == "" ; /D= was not used on the command line
        ${If} "${SUFFIX}" == "32"
            StrCpy $INSTDIR "$PROGRAMFILES\Audiveris${SUFFIX}"
        ${Else}
            StrCpy $INSTDIR "$PROGRAMFILES64\Audiveris${SUFFIX}"
        ${EndIf}
    ${EndIf}
FunctionEnd

Section "Hauptgruppe" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File /r "${PROJECT_DIR}\build\installers\${TARGET_OS}\*"
 
  SetOutPath "$PROGRAMFILES\tesseract-ocr\tessdata"
  SetOverwrite ifnewer
  File "C:\Program Files (x86)\tesseract-ocr\tessdata\*.*"

  ; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
  
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON1 "Do you want to associate '.omr' file extension with Audiveris?" IDNO noAsso
    ${registerExtension} "$INSTDIR\bin\${PRODUCT_NAME}.bat" ".omr" "OpticalMusicRecognition_File" "$INSTDIR\bin\icon-256.ico"
  noAsso:
SectionEnd

Section -AdditionalIcons
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Audiveris.lnk" "$INSTDIR\bin\${PRODUCT_NAME}.bat" "" "$INSTDIR\bin\icon-256.ico"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) has been properly uninstalled."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Do you want to uninstall $(^Name)?" IDYES +2
  Abort
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Do you want to keep the configuration data?" IDYES +2
  RmDir /r "$AppData\AudiverisLtd\audiveris"
FunctionEnd

!macro BadPathsCheck
StrCpy $R0 $INSTDIR "" -2
StrCmp $R0 ":\" bad
StrCpy $R0 $INSTDIR "" -14
StrCmp $R0 "\Program Files" bad
StrCpy $R0 $INSTDIR "" -19
StrCmp $R0 "\Program Files(x86)" bad
StrCpy $R0 $INSTDIR "" -8
StrCmp $R0 "\Windows" bad
StrCpy $R0 $INSTDIR "" -6
StrCmp $R0 "\WinNT" bad
StrCpy $R0 $INSTDIR "" -9
StrCmp $R0 "\system32" bad
StrCpy $R0 $INSTDIR "" -8
StrCmp $R0 "\Desktop" bad
StrCpy $R0 $INSTDIR "" -23
StrCmp $R0 "\Documents and Settings" bad
StrCpy $R0 $INSTDIR "" -13
StrCmp $R0 "\My Documents" bad done
bad:
  MessageBox MB_OK|MB_ICONSTOP "Install path invalid!"
  Abort
done:
!macroend

Section Uninstall
  ; Check that the uninstall isn't dangerous.
  !insertmacro BadPathsCheck

  ; Delete installed software
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\lib\*"
  Delete "$INSTDIR\bin\*"
  RMDir "$INSTDIR\lib"
  RMDir "$INSTDIR\bin"
  RMDir "$INSTDIR"

  ; Retrieve start menu folder from registry then delete folder and registry key
  !insertmacro MUI_STARTMENU_GETFOLDER Application $ICONS_GROUP
  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Website.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Audiveris.lnk"
  RMDir "$SMPROGRAMS\$ICONS_GROUP"
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"

  ${unregisterExtension} ".omr" "OpticalMusicRecognition_File"
  DeleteRegKey HKCR "OpticalMusicRecognition_File" ; brute force delete

  SetAutoClose true
SectionEnd